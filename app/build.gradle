apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

detekt {
    version = "1.0.0.RC8"
    defaultProfile {
        input = file("src/main/java")
        config = "$rootDir/default-detekt-config.yml"
        filters = ".*/resources/.*,.*/build/.*"
        output = "$buildDir/reports/detekt"
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.github.getsixtyfour.openpyn"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }
    buildTypes {
        release {
            ndk {
                abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // Comment next two lines to run "fastlane beta"
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
    }
    aaptOptions {
        noCompress 'mbtiles'
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning', 'MissingRegistered', 'UnusedIds', 'UnusedResources'
        checkAllWarnings true
        xmlReport false
        abortOnError false
        baseline file("lint-baseline.xml")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        animationsDisabled = true
    }
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
        preferProjectModules()
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'src/main/libs')
    // Android
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    // Anko
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"
    implementation "org.jetbrains.anko:anko-sdk27-listeners:$anko_version"
    // Anko Layouts
    implementation "org.jetbrains.anko:anko-sdk27:$anko_version"
    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1'
    // Emoji
    implementation('com.vdurmont:emoji-java:4.0.0') {
        exclude group: 'org.json', module: 'json'
    }
    // Geocoder
    implementation 'de.westnordost:countryboundaries:1.4'
    // JSON
    implementation 'com.squareup.moshi:moshi:1.8.0'
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // Logger
    implementation 'io.github.jupf.staticlog:staticlog:2.2.0'
    // Maps
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    // Networking
    implementation 'com.github.1951FDG:juicessh-pluginlibrary:1.0.14'
    implementation 'com.github.kittinunf.fuel:fuel-android:1.13.0'
    // UI
    implementation('com.afollestad.material-dialogs:core:0.9.6.0') {
        exclude group: 'me.zhanghai.android.materialprogressbar'
    }
    implementation('com.github.1951FDG:Android-Multi-Select-Dialog:v3.0') {
        exclude group: 'androidx.multidex'
    }
    implementation('com.github.1951FDG:ProgressToolbar:1.0.5') {
        exclude group: 'me.zhanghai.android.materialprogressbar'
    }
    implementation 'com.github.AdityaAnand1:Morphing-Material-Dialogs:0.0.4'
    implementation 'com.github.mayuroks:minibar:0.2.0'
    implementation 'com.github.tingyik90:snackprogressbar:5.0'
    implementation 'com.github.daniel-stoneuk:material-about-library:2.4.2'
    implementation('com.github.armcha:ElasticView:0.2.0') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    }
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.0'
    // Other
    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.7'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
    // Test
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1'
    androidTestImplementation('com.schibsted.spain:barista:2.7.0') {
        exclude group: 'androidx.test.espresso'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    }
    androidTestImplementation('tools.fastlane:screengrab:1.2.0') {
        exclude group: 'androidx.test'
        exclude group: 'androidx.test.espresso'
    }
}
