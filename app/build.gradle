apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'androidx.navigation.safeargs.kotlin'

apply plugin: 'com.google.android.gms.oss-licenses-plugin'

apply plugin: 'com.google.android.gms.strict-version-matcher-plugin'

apply plugin: 'com.getkeepsafe.dexcount'

apply plugin: 'com.github.ben-manes.versions'

apply plugin: 'io.fabric'

apply plugin: 'com.github.konifar.gradle.unused-resources-remover'

apply plugin: 'com.gladed.androidgitversion'

apply plugin: 'io.gitlab.arturbosch.detekt'

apply plugin: 'net.ltgt.errorprone'

//apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId 'io.github.getsixtyfour.openpyn'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }
        resConfigs 'en'
    }
    buildTypes {
        release {
            ndk {
                abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            }
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro',
                    'proguard-rules-release.pro'
        }
        debug {
            // Comment next two lines to run "fastlane beta"
            ext.alwaysUpdateBuildId = false
            ext.enableCrashlytics = false
            ndk {
                abiFilters 'armeabi-v7a'
            }
            applicationIdSuffix ".debug"
        }
    }
    buildFeatures {
        buildConfig true
        viewBinding true
        dataBinding false
        aidl true
        renderScript false
        resValues true
        shaders false
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
            version '3.10.2'
        }
    }
    sourceSets {
        debug.java.srcDirs += 'src/debug/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
    }
    aaptOptions {
        noCompress 'mbtiles'
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        // check 'Registered', 'VectorPath', 'DuplicateStrings', 'UnknownNullness', 'UnusedResources', 'UnusedIds'
        abortOnError false
        baseline file('lint-baseline.xml')
        checkAllWarnings true
        disable 'GoogleAppIndexingWarning'
        lintConfig file('lint.xml')
        xmlReport false
    }
    packagingOptions {
        exclude 'META-INF/kotlinx-coroutines-core.kotlin_module'
        exclude 'META-INF/kotlinx-coroutines-io.kotlin_module'
        exclude 'META-INF/kotlinx-io.kotlin_module'
        exclude 'lib/arm64-v8a/libsqliteX.so'
        exclude 'lib/armeabi-v7a/libsqliteX.so'
        exclude 'lib/x86/libsqliteX.so'
        exclude 'lib/x86_64/libsqliteX.so'
        exclude 'lib/arm64-v8a/libsqlite4java-android.so'
        exclude 'lib/armeabi-v7a/libsqlite4java-android.so'
        exclude 'lib/x86/libsqlite4java-android.so'
        exclude 'lib/x86_64/libsqlite4java-android.so'
    }
    testOptions {
        animationsDisabled true
    }
}

androidGitVersion {
    format '%describe%%-branch%%-dirty%'
}

detekt {
    toolVersion = '1.5.1'
    input = files('src/main/kotlin')
    baseline = file('detekt-baseline.xml')
    config = files("$rootDir/config/detekt/detekt.yml")
    reports {
        xml {
            enabled = false
            destination = file("$buildDir/reports/detekt/detekt.xml")
        }
        html {
            enabled = true
            destination = file("$buildDir/reports/detekt/detekt.html")
        }
    }
}

dexcount {
    enabled = false
}

configurations {
    implementation {
        exclude group: 'org.jetbrains.kotlinx', module: 'atomicfu'
        exclude group: 'org.json', module: 'json'
    }

    /*debugAndroidTestCompileClasspath {
        exclude group: 'androidx.annotation'
        exclude group: 'androidx.appcompat'
        exclude group: 'androidx.collection'
        exclude group: 'androidx.core'
        exclude group: 'androidx.fragment'
        exclude group: 'androidx.lifecycle'
        exclude group: 'androidx.recyclerview'
        exclude group: 'androidx.savedstate'
        exclude group: 'androidx.transition'
        exclude group: 'com.google.firebase', module: 'firebase-common'

        resolutionStrategy {
            failOnVersionConflict()
        }
    }*/
}

dependencies {
    // Java 8+ libraries
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.5'

    // 1951FDG
    implementation 'com.github.1951FDG:Android-Multi-Select-Dialog:3.8'
    implementation('com.github.1951FDG:ProgressToolbar:1.0.8') {
        exclude group: 'me.zhanghai.android.materialprogressbar'
    }
    implementation 'com.github.1951FDG:juicessh-pluginlibrary:1.0.16'
    debugImplementation 'com.github.1951FDG.AndroidPerformanceMonitor:blockcanary-android:slave-SNAPSHOT'

    // Android
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.sqlite:sqlite:2.1.0'

    // Architecture
    implementation('com.naver.android.svc:svc:1.0.1') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    }
    //kapt 'com.naver.android.svc:svc-compiler:1.0.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.firebase:firebase-analytics:17.2.2'

    // GDPR
    implementation 'com.github.MFlisar:GDPRDialog:1.4.2'

    // Google Play Services
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation 'com.google.android.play:core:1.6.5'

    // Google Material Design
    implementation 'com.google.android.material:material:1.1.0'

    // HTTP Client
    implementation("io.ktor:ktor-client-android:$ktor_version") {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-common'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core'
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core-common'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    // JSON
    implementation('com.jayrave:moshi-pristine-models:1.0.0') {
        transitive = false
    }
    implementation 'com.squareup.moshi:moshi:1.9.2'

    // Kotlin
    implementation('org.jetbrains.kotlin:kotlin-stdlib') {
        version {
            strictly "$kotlin_version"
        }
    }
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8') {
        version {
            strictly "$kotlin_version"
        }
    }

    // LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'

    // Logger
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'io.github.microutils:kotlin-logging:1.7.9'
    debugImplementation 'at.favre.lib:slf4j-timber:1.0.1'
    debugImplementation ('com.github.AppDevNext:Logcat:1.9.0') {
        exclude group: 'com.google.android.material'
        exclude group: 'com.jakewharton.timber'
    }
    testImplementation "org.slf4j:slf4j-simple:$slf4j_version"

    // Permission
    implementation 'pub.devrel:easypermissions:3.0.0'

    // Preference
    implementation('net.mm2d:preference:0.2.5') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    }

    // UI
    implementation 'com.github.GrenderG:Toasty:1.4.2'
    implementation 'com.github.mayuroks:minibar:0.2.0'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.6.1'

    // Stetho
    debugImplementation ('com.facebook.stetho:stetho-timber:1.5.1') {
        exclude group: 'com.jakewharton.timber'
    }

    // Error Prone
    debugAnnotationProcessor 'com.uber.nullaway:nullaway:0.7.9'
    debugCompileOnly 'com.jakewharton.nopen:nopen-annotations:1.0.1'
    errorprone 'com.jakewharton.nopen:nopen-checker:1.0.1'
    errorprone 'com.google.errorprone:error_prone_core:2.3.4'
    //noinspection GradleDynamicVersion
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'

    // Test
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation('com.schibsted.spain:barista:3.2.0') {
        exclude group: 'androidx.test.espresso'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    }
    androidTestImplementation('tools.fastlane:screengrab:1.2.0') {
        exclude group: 'androidx.test'
        exclude group: 'androidx.test.espresso'
        exclude group: 'com.android.support.test.uiautomator'
    }

    //noinspection GradleDependency
    implementation('com.github.tingyik90:snackprogressbar:6.1.1') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-android-extensions-runtime'
    }
    //noinspection GradleDependency
    implementation 'commons-cli:commons-cli:1.4'

    // Google Maps SDK for Android v.3.0.0 BETA
    //implementation name:'maps-sdk-3.0.0-beta', ext:'aar'
    //implementation 'com.google.android.gms:play-services-gcm:17.0.0'
}

apply from: 'other.gradle'
